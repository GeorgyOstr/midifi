var fluid: FluidData;
var font: Font;

gen error_msg: unit -> string = '::std::string(strerror(errno))';
fun /(l: string, r: string) => Filename::join (l, r);

object RootWindow() = {
    midi := #MidiFile::MidiFile;
    path := '/home/ryan/Documents/piano_req.midi.mid';
    this := Filename::dirname$ System::argv 0;
    if System::argc == 2 perform
        path = System::argv 1;

    if midi.read path == 0 do
        eprintln$ f"Error opening %S: %S" (path, #error_msg);
        System::exit 1;
    done

    ps := PlayerScreen midi;
    // XXX: Custom soundfonts should probably be an option.
    fluid = FluidData (path, this/'data'/'FluidR3_GM.sf2', midi.tpq, numerator,
                       deltatime);

    // XXX: The parens here are due to felix-lang/felix#77.
    font = #Font;
    if not (font&.load_from_file$ this/'data'/'Arial.ttf') do
        eprintln 'Cannot open font data!!\n';
        System::exit 1;
    done

    var w = RenderWindow (values.video_mode, 'midifi');
    show_fps := Env::getenv 'SHOWFPS' == 'true';

    method proc run() {
        fluid.play;

        cstmt 'sf::Clock fpsc;';

        while w.isopen do
            var event: Event;
            while w.poll_event &event perform
                if event.type == EventType::Closed do
                    w.close;
                    return;
            done

            if not fluid.playing do
                w.close;
                return;
            done

            if show_fps do
                fps := cexpr[float]
                    '1000000.0f / fpsc.restart().asMicroseconds()'
                endcexpr;
                print$ f"FPS: %S\r" fps.str;
            done

            w.clear values.gray_color;
            draw_onto (ps, w);
            w.display;
        done
    }
}
